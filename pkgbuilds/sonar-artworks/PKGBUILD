# Maintainer: Stanis≈Çaw Przedzinkowski <stasp@poczta.sytes.net>

_dist='Sonar'

pkgbase=sonar-artworks
pkgname=('sonar-common' 'sonar-backgrounds' 'sonar-calamares-branding' 'sonar-sound-theme')
pkgver=2020.07.23
pkgrel=1
arch=('any')
url="https://github.com/blind-computing/sonar-artworks"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')


package_sonar-calamares-branding() {
	pkgdesc='Sonar Linux calamares branding'
	depends=('calamares')

	install -d ${pkgdir}/usr/share/calamares/branding/sonar
	cp ${srcdir}/${pkgbase}/calamares-branding/* ${pkgdir}/usr/share/calamares/branding/sonar

	sed -e "s|@version@|${pkgver}|g" -e "s|@dist@|${_dist}|g" \
		-i ${pkgdir}/usr/share/calamares/branding/sonar/branding.desc

	sed -e "s|@dist@|${_dist}|g" -i ${pkgdir}/usr/share/calamares/branding/sonar/show.qml
}

package_sonar-backgrounds() {
	pkgdesc='Sonar Linux backgrounds'

	install -d ${pkgdir}/usr/share/backgrounds
	cp ${srcdir}/${pkgbase}/backgrounds/* ${pkgdir}/usr/share/backgrounds

}

package_sonar-sound-theme() {
	pkgdesc='Sonar Linux sound theme'

	install -d "$pkgdir/usr/share"
	cp -r ${srcdir}/${pkgbase}/sounds $pkgdir/usr/share
}

package_sonar-common() {
	pkgdesc='Sonar Linux common'

	install -d "$pkgdir/usr/share"
	cp -r ${srcdir}/${pkgbase}/common/{applications,sonar} $pkgdir/usr/share
}
